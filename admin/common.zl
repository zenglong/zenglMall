// 该脚本是后台管理页面脚本都会加载的一个公共脚本，该脚本会执行一些公共的初始化操作，
// 例如加载config.zl配置脚本，加载mysql.zl数据类脚本，加载fatal_error.zl设置运行时错误回调函数的脚本，
// 以及判断当前用户是否登录过后台，如果没有登录过则跳转到后台登录页面，还定义了init_db之类的通用脚本函数
use builtin, request, mysql, session;
def TRUE 1;
def FALSE 0;
inc '../config.zl';
inc '../mysql.zl';
inc '../fatal_error.zl';

cookies = rqtGetCookie();
sess_data = sessGetData(bltStr(&cookies['MALL_ADM_SESSION']));
if(bltCount(sess_data) == 0 || !sess_data['uid'])
	rqtSetResponseHeader("HTTP/1.1 302 Moved Temporarily");
	rqtSetResponseHeader("Location: login.zl");
	rqtSetResponseHeader("Cache-Control: no-store,no-cache,must-revalidate,post-check=0,pre-check=0");
	rqtSetResponseHeader("Pragma: no-cache");
	bltExit();
endif

// 初始化数据库连接
fun init_db()
	global config;
	db = bltArray();
	Mysql.init(db, config, "tpl/error.tpl");
	return db;
endfun

// ajax返回成功或失败
fun ajax_return(errmsg = '')
	ret['msg'] = errmsg ? 'failed' : 'success';
	ret['errmsg'] = errmsg;
	if(errmsg != '')
		ret['error'] = errmsg;
	endif
	rqtSetResponseHeader("Content-Type: application/json");
	print bltJsonEncode(ret);
	bltExit();
endfun
