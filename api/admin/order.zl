inc 'common.zl';

Order.action(rqtGetQuery());

class Order
	fun action(querys)
		action = querys['act'] ? querys['act'] : 'list';
		if(action == 'list')
			self.list();
		elif(action == 'view')
			self.view(bltInt(querys['id']));
		elif(action == 'modify_seller_remark')
			self.modifySellerRemark();
		elif(action == 'set_send_status')
			self.setSendStatus();
		else
			common_json_error('无效的请求');
		endif
	endfun

	// 订单列表
	fun list()
		db = init_db();
		posts = rqtGetBodyAsArray();
		where = " where 1 ";
		oid = bltStr(&posts['oid']);
		name = bltStr(&posts['name']);
		username = bltStr(&posts['username']);
		status = bltStr(&posts['status']);
		// 根据订单号搜索订单列表
		if(oid)
			where += ' and o.oid like "%' + Mysql.Escape(db, oid) + '%"';
		endif
		// 根据商品名称搜索订单列表
		if(name)
			where += ' and o.name like "%' + Mysql.Escape(db, name) + '%"';
		endif
		// 根据下单用户的用户名搜索订单列表
		if(username)
			where += ' and u.username like "%' + Mysql.Escape(db, username) + '%"';
		endif
		// 根据订单状态搜索订单列表
		if(status)
			where += ' and o.status = "' + Mysql.Escape(db, status) + '"';
		endif
		tmp = Mysql.fetchOne(db, "select count(1) as cnt from orders as o inner join users as u on u.id=o.uid " + where);
		data['total'] = bltInt(tmp['cnt']);
		page = bltInt(posts['page']);
		pageSize = bltInt(posts['pageSize']);
		limit = pageSize <= 0 ? 10 : pageSize;
		offset = page <= 0 ? 0 : (page - 1) * limit;
		data['list_data'] = Mysql.fetchAll(db, "select o.id,o.oid,o.name,o.thumbnail,o.amount,o.num,o.status,o.uid,u.username from orders as o inner join users as u on u.id=o.uid " + 
				where + " order by o.id desc limit " + offset + "," + limit);
		common_json_success(data);
	endfun

	// 订单详情
	fun view(id)
		db = init_db();
		data['info'] = Mysql.fetchOne(db, "select * from orders where id = " + id);
		if(data['info', 'sku_attr_info'] != '')
			data['info', 'sku_attr_info'] = bltJsonDecode(data['info', 'sku_attr_info']);
		endif
		common_json_success(data);
	endfun

	// 设置订单的商家备注信息
	fun modifySellerRemark()
		posts = rqtGetBodyAsArray();
		data['seller_remark'] = bltStr(posts['seller_remark']);
		data['update_time'] = bltDate("%Y-%m-%d %H:%M:%S");
		Mysql.Update(init_db(), 'orders', data, 'id=' + bltInt(posts['id']));
		common_json_success(NULL, "设置成功");
	endfun

	// 将订单设置为待收货状态(也就是已发货状态)
	fun setSendStatus()
		posts = rqtGetBodyAsArray();
		id = bltInt(posts['id']);
		db = init_db();
		order_info = Mysql.fetchOne(db, "select * from orders where id = " + id);
		if(!bltCount(order_info))
			common_json_error('订单信息不存在');
		endif
		if(order_info['status'] != 'WAIT_SELLER_SEND')
			common_json_error('只能将待发货的订单设置为待收货状态');
		endif
		data['status'] = 'WAIT_BUYER_CONFIRM';
		data['send_time'] = data['update_time'] = bltDate("%Y-%m-%d %H:%M:%S");
		Mysql.Update(db, 'orders', data, 'id=' + id);
		common_json_success(NULL, "设置成功");
	endfun
endclass

